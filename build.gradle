plugins {
    id "com.github.hierynomus.license" version "0.16.1"
}

allprojects {
    apply plugin: 'license'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    group = 'com.anjlab.tapestry5'

    license {
        header rootProject.file('LICENSE')
        skipExistingHeaders true
        strictCheck true
        exclude '*.tml'
    }
}

project.ext.tapestry_version = '5.9.0-preview'

configurations {
    all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    all*.exclude group: 'org.slf4j', module: 'slf4j-simple'
    all*.exclude group: 'log4j', module: 'log4j'
    all*.exclude group: 'commons-logging', module: 'commons-logging'
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 11
    targetCompatibility = 11

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url 'https://repository.apache.org/content/repositories/staging'
        }
    }

    dependencies {
        def slf4j_version = '1.7.5'
        def logback_version = '1.0.13'

        api "org.slf4j:slf4j-api:${slf4j_version}"
        implementation "org.slf4j:jcl-over-slf4j:${slf4j_version}"
        implementation "org.slf4j:log4j-over-slf4j:${slf4j_version}"

        compileOnly "ch.qos.logback:logback-core:${logback_version}"
        compileOnly "ch.qos.logback:logback-classic:${logback_version}"

        testImplementation 'junit:junit:4.8.2'
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        from sourceSets.main.allSource
        archiveClassifier.set('sources')
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        from javadoc.destinationDir
        archiveClassifier.set('javadoc')
    }

    jar.dependsOn sourcesJar
    jar.dependsOn javadocJar

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    def pomFragments = {
        developers {
            developer {
                id "dmitrygusev"
                name "Dmitry Gusev"
                email "dmitry@anjlab.com"
            }
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar

                pom.withXml {
                    def root = asNode()

                    root.appendNode('description', project.description)

                    def license = root.appendNode('licenses').appendNode('license')
                    license.appendNode("name", "The Apache Software License, Version 2.0")
                    license.appendNode("url", "http://www.apache.org/licenses/LICENSE-2.0.txt")
                    license.appendNode("distribution", "repo")

                    root.children().last() + pomFragments
                }
            }
        }
    }
}
